apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'osgi'
apply plugin: 'maven'

version = '1.0.0'
group = 'org.openmuc'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories { mavenCentral() }

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.10'
  compile files('dependencies/rxtxcomm_api-2.2pre2-11_bundle.jar')
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: project.properties.repository) {
        authentication(userName: project.properties.artifactoryUser, password: project.properties.artifactoryPass)
      }
      snapshotRepository(url: project.properties.snapshotRepository) {
        authentication(userName: project.properties.artifactoryUser, password: project.properties.artifactoryPass)
      }
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
   classifier = 'sources'
   from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
   archives sourcesJar
   archives javadocJar
}

jar {
  manifest {
    version = project.version.replace('-','.');
    name = "jMBus"
    instruction 'Export-Package', '!*.internal.*,*'
  }
}

eclipse.pathVariables([GRADLE_USER_HOME:file(gradle.gradleUserHomeDir)])
tasks.eclipse.dependsOn(cleanEclipse)

task javadocs(type: Javadoc) {
  source sourceSets.main.allJava
  
  exclude '**/internal/**'
  
  destinationDir = new File(buildDir, 'javadoc')
  
  classpath = sourceSets.main.compileClasspath
  classpath += sourceSets.main.output
}

task (tar, type: Tar) {
  dependsOn(build)
  dependsOn(javadocs)

  compression = Compression.GZIP

  into(project.archivesBaseName) {
    from('./') {
      include 'doc/**/*'
      include 'licenses/**/*'
      include 'build/libs/**/*'
      include 'build/javadoc/**/*'
      include 'build.gradle'
      include 'dependencies/**/*'
      include 'src/**/*'
    }
  }
}
